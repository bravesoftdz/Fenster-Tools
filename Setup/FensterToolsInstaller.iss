; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppID={{B9D63C14-BB77-49A8-B450-98A5567088C4}
UsePreviousAppDir=true
;CreateUninstallRegKey=false
;UpdateUninstallLogAppName=false
AppName=Fenster-Tools
AppVersion=5.0.9.3
;AppVerName=Fenster-Tools 5.0.9.3
AppPublisher=Adrian Zinke
AppPublisherURL=http://www.adrian-zinke.de/
AppSupportURL=http://www.adrian-zinke.de/
AppUpdatesURL=http://www.adrian-zinke.de/
DefaultDirName={pf}\FensterTools
DefaultGroupName=Fenster-Tools
AllowNoIcons=true
LicenseFile=C:\Delphi\Sinnvolles\FensterToolsLazarus\Setup\License.txt
InfoBeforeFile=C:\Delphi\Sinnvolles\FensterToolsLazarus\Setup\BeforeInstall.txt
InfoAfterFile=C:\Delphi\Sinnvolles\FensterToolsLazarus\Setup\AfterInstall.txt
OutputDir=C:\Delphi\Sinnvolles\FensterToolsLazarus\Setup
OutputBaseFilename=FensterToolsSetup                                                     
Compression=lzma/Max
SolidCompression=true
VersionInfoVersion=5.0.9.3
ShowLanguageDialog=auto
AllowCancelDuringInstall=false
UninstallDisplayIcon={app}\FensterTools.exe
MinVersion=6.0.6000
DisableDirPage=auto
DisableProgramGroupPage=auto
AppMutex=FensterTools
SetupIconFile=C:\Delphi\Sinnvolles\FensterToolsLazarus\Icon\fenstertools256x256_compressed.ico
AppCopyright=Adrian Zinke

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"

[Files]
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\libpcre2-16.dll; DestDir: {app}; Flags: IgnoreVersion overwritereadonly replacesameversion; 
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\Service\libcrypto-37.dll; DestDir: {app}; Languages: english german; Flags: IgnoreVersion overwritereadonly replacesameversion; 
; Source: "C:\Delphi\Sinnvolles\FensterTools\ARFIX.dll"; DestDir: "{code:GetDestDir}"; Flags: ignoreversion replacesameversion overwritereadonly
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\FensterTools.exe; DestDir: {app}; DestName: FensterTools.exe; Flags: ignoreversion replacesameversion overwritereadonly; 
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\Service\FTHelperSvc32.exe; DestDir: {app}; Flags: IgnoreVersion overwritereadonly replacesameversion;  
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\Service\FTHelperSvc64.exe; DestDir: {app}; Flags: IgnoreVersion overwritereadonly replacesameversion; 
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\Icons\view_refresh_7.ico; DestDir: {app}\Icons; Languages: english german; 
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\Icons\software-update-download.ico; DestDir: {app}\Icons; Languages: english german; 
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\Icons\mark.ico; DestDir: {app}\Icons; Languages: english german; 
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\Icons\dialog-close.ico; DestDir: {app}\Icons; Languages: english german; 
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\Icons\Empty.png; DestDir: {app}\Icons; 
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\Changelog.txt; DestDir: {app}; Flags: ignoreversion replacesameversion overwritereadonly; 
Source: C:\Delphi\Sinnvolles\FensterToolsLazarus\FensterToolsLog.exe; DestDir: {app}; DestName: FensterToolsLog.exe; Flags: ignoreversion replacesameversion overwritereadonly; 

[Dirs]
Name: "{userappdata}\FensterTools"; Permissions: users-modify; Flags: uninsalwaysuninstall; Check: (not WizardSilent);

[Icons]
Name: {group}\{#emit SetupSetting("AppName")}; Filename: {app}\FensterTools.exe; 
Name: "{group}\{cm:UninstallProgram,{#emit SetupSetting("AppName")}}"; Filename: "{uninstallexe}"

[Run]
Filename: {app}\FensterTools.exe; Description: {cm:LaunchProgram,{#emit SetupSetting("AppName")}}; Parameters: {code:GetExeStartParams}; Flags: nowait postinstall runasoriginaluser ShellExec; Check: (not WizardSilent);
Filename: {app}\FensterTools.exe; Description: {cm:LaunchProgram,{#emit SetupSetting("AppName")}}; Parameters: {code:GetExeStartParams}; Flags: nowait runasoriginaluser ShellExec; Check: (WizardSilent);
Filename: {app}\FTHelperSvc32.exe; StatusMsg: {cm:RegisterHelperSvc32}; Parameters: "-i"; Flags: waituntilterminated runhidden runascurrentuser
Filename: {app}\FTHelperSvc64.exe; StatusMsg: {cm:RegisterHelperSvc64}; Parameters: "-i"; Flags: waituntilterminated runhidden runascurrentuser
;Flags: runhidden;  

[CustomMessages]
;english.Autostart=Start {#emit SetupSetting("AppName")} automatically when you turn on your pc
;german.Autostart={#emit SetupSetting("AppName")} beim Hochfahren automatisch starten
english.RegisterHelperSvc32=Registering 32-bit Helper Service (can take up to 2 minutes) ...
german.RegisterHelperSvc32=Registriere 32-bit Helper Service (kann bis zu 2 Minuten dauern) ...
german.RegisterHelperSvc64=Registriere 32-bit Helper Service (kann bis zu 2 Minuten dauern) ...
english.RegisterHelperSvc64=Registering 64-bit Helper Service (can take up to 2 minutes) ...
english.UnregisterHelperSvc32=Unregistering 32-bit Helper Service ...
german.UnregisterHelperSvc32=Melde 32-bit Helper Service ab ...
german.UnregisterHelperSvc64=Melde 32-bit Helper Service ab ...
english.UnregisterHelperSvc64=Unregistering 64-bit Helper Service ...
german.SetupFailed=Die Installation ist fehlgeschlagen.
english.SetupFailed=Installation failed.

[Tasks]
;Name: Autostart; Description: "{cm:Autostart}";

[Registry]
Root: HKLM; Subkey: "Software\Fenster-Tools"; Flags: uninsdeletekey
Root: HKLM; Subkey: "Software\Fenster-Tools"; ValueType: string; ValueName: "InstallPath"; ValueData: "{app}"
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "FensterTools"; ValueData: "{app}\FensterTools.exe"; Flags: uninsdeletevalue; Check: (not WizardSilent);
;CreateUninstallRegKey

[UninstallDelete]
;Type: files; Name: "{userappdata}\FensterTools\*"
Type: filesandordirs; Name: "{userappdata}\FensterTools"

[InstallDelete]
Type: files; Name: "{userappdata}\FensterTools\*.log"; Check: (not WizardSilent);
Type: files; Name: "{app}\Empty.png"
Type: files; Name: "{app}\PatchHelper64.exe"
Type: files; Name: "{app}\Icons\check.png"
Type: files; Name: "{app}\Icons\cross.png"

[Code]
var 
  isVerySilent: boolean;
(*
procedure InstallHelperService32;
var
  ResultCode: Integer;
begin
  Exec('FTHelperSvc32.exe', '-i', ExpandConstant('{app}'), SW_SHOW, ewWaitUntilTerminated, ResultCode);
end;
procedure InstallHelperService64;
var
  ResultCode: Integer;
begin
  Exec('FTHelperSvc64.exe', '-i', ExpandConstant('{app}'), SW_SHOW, ewWaitUntilTerminated, ResultCode);
end;*)
(*
function GetDestDir(Param: String): String;
var
  InstallPath: String;
begin
  if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'Software\Fenster-Tools', 'InstallPath', InstallPath) then
  begin
    Result := InstallPath;
  end else Result := ExpandConstant('{app}');
end;
*)
function GetExeStartParams(Param: String): String;
begin
  if WizardSilent then
  begin
    if isVerySilent then
      Result := '/updated'
    else
      Result := '/updated /show'
  end else Result := '';
end;
{
function ShouldSkipPage(PageID: Integer): Boolean;
var
  InstallPath: String;
begin
  Result := false;
  if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'Software\Fenster-Tools', 'InstallPath', InstallPath) then
  begin
    if (PageID = wpSelectDir) or (PageID = wpSelectProgramGroup) or (PageID = wpSelectTasks) then
    begin
      Result := true;
    end;
  end;
end;

function UpdateReadyMemo(Space, NewLine, MemoUserInfoInfo, MemoDirInfo, MemoTypeInfo, MemoComponentsInfo, MemoGroupInfo, MemoTasksInfo: String): String;
var
  InstallPath: String;
begin
  if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'Software\Fenster-Tools', 'InstallPath', InstallPath) then
  begin
    Result := 'FensterTools ist bereits auf ihrem PC installiert.'+NewLine+'Deshalb werden die alten Einstellungen übernommen'+NewLine+'und nur ein Update durchgeführt.';
  end;
end;}

function CreateMutex(lpMutexAttributes: Integer; bInitialOwner: BOOL; lpName: String): Cardinal; external 'CreateMutexW@kernel32.dll stdcall';
function WaitForSingleObject(hHandle, dwMilliseconds: Cardinal): Cardinal; external 'WaitForSingleObject@kernel32.dll stdcall';
function ReleaseMutex(hMutex: Cardinal): BOOL; external 'ReleaseMutex@kernel32.dll stdcall';
function CloseHandle(hHandle: Cardinal): BOOL; external 'CloseHandle@kernel32.dll stdcall';

function InitializeSetup(): Boolean;
var
  WaitResult, SingleInstanceMutex: Cardinal;
  j: Cardinal;
begin
  isVerySilent := false;
  if WizardSilent then
  begin
    SingleInstanceMutex := CreateMutex(0, True, 'FensterTools');
    WaitResult := WaitForSingleObject(SingleInstanceMutex, 10000);
    if (WaitResult = 258 {WAIT_TIMEOUT}) or (WaitResult = $FFFFFFFF {WAIT_FAILED}) then
    begin
      MsgBox('Die Installation des FensterTools-Updates ist fehlgeschlagen. Bitte installieren Sie das Update manuell.', mbInformation, MB_OK);
      Result := false;
    end else begin
      ReleaseMutex(SingleInstanceMutex);
      CloseHandle(SingleInstanceMutex);
      Result := true;
    end;

    for j := 1 to ParamCount do
    begin
      // verysilent has the purpose to leave out the /show parameter when restarting FensterTools
      if CompareText(ParamStr(j), '/verysilent') = 0 then
      begin
        isVerySilent := true
        break;
      end;
    end;
  end else Result := true;
end;
 
{function PrepareToInstall(var NeedsRestart: Boolean): String;
begin
  Result := 'Failure';
  MsgBox('Die Installation des FensterTools-Updates ist fehlgeschlagen.', mbInformation, MB_OK);
end;}

[UninstallRun]
Filename: {app}\FTHelperSvc32.exe; Parameters: -u; StatusMsg: {cm:UnregisterHelperSvc32}; 
Filename: {app}\FTHelperSvc64.exe; Parameters: -u; StatusMsg: {cm:UnregisterHelperSvc64};

[InnoIDE_PostCompile]
Name: C:\Delphi\Sinnvolles\FensterToolsLazarus\Setup\SignTool\SignTool.exe; Parameters: "-f ""C:\Delphi\Sinnvolles\FensterToolsLazarus\Setup\FensterToolsSetup.exe"" -k ""C:\Delphi\Sinnvolles\FensterToolsLazarus\Setup\private-key.rsa"" --base64";
